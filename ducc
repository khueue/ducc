#!/usr/bin/env escript
%%! -pa ebin -Wall

version() ->
    '0.4.1'.

write_version() ->
    io:fwrite('ducc ~s~n', [version()]).

usage() ->
    write_version(),
    io:format('usage: ducc [-a|-c|-l|-p|-t|-v] [file]~n').

main(["-v"]) ->
    write_version();
main(["-"++Initial, File]) ->
    {Lines, String} = read_lines_and_string(File),
    compile(program(Initial), File, String, Lines);
main(["-"++Initial]) ->
    {Lines, String} = read_lines_and_string(standard_io),
    compile(program(Initial), '<stdin>', String, Lines);
main([File]) ->
    {Lines, String} = read_lines_and_string(File),
    compile(program(default), File, String, Lines);
main([]) ->
    {Lines, String} = read_lines_and_string(standard_io),
    compile(program(default), '<stdin>', String, Lines);
main(_) ->
    usage().

program("l")  -> lexer;
program("p")  -> parser;
program("a")  -> analyzer;
program("t")  -> translator;
program("c")  -> codegen;
program(_Def) -> codegen.

read_lines_and_string(standard_io) ->
    Lines = tool_chain:read_lines_from_stream(standard_io),
    String = tool_chain:string_from_lines(Lines),
    {Lines, String};
read_lines_and_string(Stream) ->
    Lines = tool_chain:read_lines_from_file(Stream),
    String = tool_chain:string_from_lines(Lines),
    {Lines, String}.

compile(Program, Stream, String, Lines) ->
    Result = (catch process(Program, Stream, String, Lines)),
    case Result of
        {ok, Output} ->
            tool_chain:term_to_output({{source,Lines}, Output});
        {lexer_exception, Message} ->
            tool_chain:die(Message);
        {parser_exception, Message} ->
            tool_chain:die(Message);
        {analyzer_exception, Message} ->
            tool_chain:die(Message);
        {translator_exception, Message} ->
            tool_chain:die(Message);
        {codegen_exception, Message} ->
            tool_chain:die(Message);
        {tool_chain_exception, invalid_term} ->
            tool_chain:die(String)
    end.

process(Program, Stream, String, Lines) ->
    case Program of
        lexer ->
            {ok, Tokens} = lexer_driver:tokenize(Stream, String),
            {ok, {tokens,Tokens}};
        parser ->
            {ok, {tokens,Tokens}} = process(lexer, Stream, String, Lines),
            {ok, ParseTree} = parser_driver:parse(Stream, Tokens),
            {ok, {ast,ParseTree}};
        analyzer ->
            {ok, {ast,ParseTree}} = process(parser, Stream, String, Lines),
            {ok, ParseTree1} = analyzer_driver:analyze(Stream, ParseTree),
            {ok, {ast,ParseTree1}};
        translator ->
            {ok, {ast,ParseTree1}} = process(analyzer, Stream, String, Lines),
            {ok, RtlCode} = translator_driver:translate(Stream, ParseTree1, Lines),
            {ok, {rtl,RtlCode}};
        codegen ->
            {ok, {rtl,RtlCode}} = process(translator, Stream, String, Lines),
            {ok, AsmCode} = codegen_driver:generate_code(Stream, RtlCode, Lines),
            {ok, {asm,AsmCode}};
        _DefaultAllSteps ->
            process(program(default), Stream, String, Lines)
    end.
